CMAKE_MINIMUM_REQUIRED(VERSION 3.0)

SET(name g3kb-switch)
PROJECT(${name})

FIND_PACKAGE(PkgConfig REQUIRED)
PKG_SEARCH_MODULE(GIO REQUIRED gio-2.0)

OPTION(G3KBSWITCH_WITH_GNOME_SHELL_EXTENSION "Build Gnome Shell extension" OFF)
IF(G3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
    ADD_DEFINITIONS(-DG3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
ENDIF()

ADD_EXECUTABLE(${name} main.c switch.c)
INCLUDE_DIRECTORIES(${GIO_INCLUDE_DIRS})
TARGET_LINK_LIBRARIES(${name} ${GIO_LDFLAGS})
#TARGET_COMPILE_OPTIONS(${name} PUBLIC -Wall)

OPTION(G3KBSWITCH_BUILD_VIM_XKBSWITCH_LIB "Build plugin for vim-xkbswitch" ON)
IF(G3KBSWITCH_BUILD_VIM_XKBSWITCH_LIB)
    SET(g3kblib g3kbswitch)
    ADD_LIBRARY(${g3kblib} SHARED switch.c switch-api.c)
    TARGET_LINK_LIBRARIES(${g3kblib} ${GIO_LDFLAGS})
    #TARGET_COMPILE_OPTIONS(${g3kblib} PUBLIC -Wall)
ENDIF()

OPTION(G3KBSWITCH_INSTALL_BASH_COMPLETION "Install bash completion script" ON)
IF(G3KBSWITCH_INSTALL_BASH_COMPLETION)
    FIND_PACKAGE(bash-completion)
    IF(BASH_COMPLETION_FOUND)
        INSTALL(FILES g3kb-switch-completion.bash
                DESTINATION ${BASH_COMPLETION_COMPLETIONSDIR}
                RENAME ${name})
    ENDIF()
ENDIF()

OPTION(G3KBSWITCH_INSTALL_ZSH_COMPLETION "Install zsh completion script" ON)
IF(G3KBSWITCH_INSTALL_ZSH_COMPLETION)
    SET(ZSH_COMPLETION_COMPLETIONSDIR "/usr/share/zsh/site-functions")
    INSTALL(FILES g3kb-switch-completion.zsh
            DESTINATION ${ZSH_COMPLETION_COMPLETIONSDIR}
            RENAME _${name})
ENDIF()

INSTALL(TARGETS ${name} ${g3kblib}
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION lib OPTIONAL)

