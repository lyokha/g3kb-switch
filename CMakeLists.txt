CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

SET(name g3kb-switch)
SET(HOMEPAGE_URL https://github.com/lyokha/g3kb-switch)
PROJECT(${name}
        LANGUAGES C
        HOMEPAGE_URL ${HOMEPAGE_URL}
        DESCRIPTION "CLI keyboard layout switcher for Gnome 3 and 4x"
        VERSION 1.3)

INCLUDE(GNUInstallDirs)
INCLUDE(CMakeDependentOption)

FIND_PACKAGE(PkgConfig REQUIRED)
PKG_SEARCH_MODULE(GIO REQUIRED gio-2.0)

SET(CMAKE_EXPORT_COMPILE_COMMANDS ON)

SET(GNOME_SHELL_EXTENSIONS_DIR
    ${CMAKE_INSTALL_DATAROOTDIR}/gnome-shell/extensions)

SET(BASH_COMPLETION_COMPLETIONSDIR "" CACHE STRING
    "The directory of bash completions, \
    empty will use the default value coming from pkg-config")
SET(ZSH_COMPLETION_COMPLETIONSDIR "" CACHE STRING
    "The directory of zsh completions, empty will use the default value")

OPTION(G3KBSWITCH_WITH_LEGACY_GNOME_SHELL_EXTENSION
    "Use Gnome Shell extension (for Gnome Shell 44 and older)" OFF)
IF(G3KBSWITCH_WITH_LEGACY_GNOME_SHELL_EXTENSION)
    ADD_DEFINITIONS(-DG3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
    INSTALL(DIRECTORY extension/g3kb-switch@g3kb-switch.org-legacy/
        DESTINATION ${GNOME_SHELL_EXTENSIONS_DIR}/g3kb-switch@g3kb-switch.org)
ENDIF()

CMAKE_DEPENDENT_OPTION(G3KBSWITCH_WITH_GNOME_SHELL_EXTENSION
    "Use Gnome Shell extension" ON
    "NOT G3KBSWITCH_WITH_LEGACY_GNOME_SHELL_EXTENSION" OFF)
IF(G3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
    ADD_DEFINITIONS(-DG3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
    INSTALL(DIRECTORY extension/g3kb-switch@g3kb-switch.org
        DESTINATION ${GNOME_SHELL_EXTENSIONS_DIR})
ENDIF()

ADD_EXECUTABLE(${name} main.c switch.c)
INCLUDE_DIRECTORIES(${GIO_INCLUDE_DIRS})
TARGET_LINK_LIBRARIES(${name} ${GIO_LDFLAGS})
#TARGET_COMPILE_OPTIONS(${name} PUBLIC -Wall)

OPTION(G3KBSWITCH_BUILD_VIM_XKBSWITCH_LIB "Build plugin for vim-xkbswitch" ON)
IF(G3KBSWITCH_BUILD_VIM_XKBSWITCH_LIB)
    SET(g3kblib g3kbswitch)
    ADD_LIBRARY(${g3kblib} SHARED switch.c switch-api.c)
    TARGET_LINK_LIBRARIES(${g3kblib} ${GIO_LDFLAGS})
    #TARGET_COMPILE_OPTIONS(${g3kblib} PUBLIC -Wall)
ENDIF()

OPTION(G3KBSWITCH_INSTALL_BASH_COMPLETION "Install bash completion script" ON)
IF(G3KBSWITCH_INSTALL_BASH_COMPLETION)
    IF(NOT BASH_COMPLETION_COMPLETIONSDIR)
        FIND_PACKAGE(bash-completion)
    ENDIF()
    IF(NOT BASH_COMPLETION_COMPLETIONSDIR)
        SET(BASH_COMPLETION_COMPLETIONSDIR
            "${CMAKE_INSTALL_DATAROOTDIR}/bash-completion/completions")
    ENDIF()
    INSTALL(FILES g3kb-switch-completion.bash
            DESTINATION ${BASH_COMPLETION_COMPLETIONSDIR}
            RENAME ${name})
ENDIF()

OPTION(G3KBSWITCH_INSTALL_ZSH_COMPLETION "Install zsh completion script" ON)
IF(G3KBSWITCH_INSTALL_ZSH_COMPLETION)
    IF(NOT ZSH_COMPLETION_COMPLETIONSDIR)
        SET(ZSH_COMPLETION_COMPLETIONSDIR
            ${CMAKE_INSTALL_DATAROOTDIR}/zsh/site-functions)
    ENDIF()
    INSTALL(FILES g3kb-switch-completion.zsh
            DESTINATION ${ZSH_COMPLETION_COMPLETIONSDIR}
            RENAME _${name})
ENDIF()

SET(G3KBSWITCH_VIM_XKBSWITCH_LIB_PATH ${CMAKE_INSTALL_LIBDIR} CACHE STRING
    "Xkbswitch library path relative to ${CMAKE_INSTALL_PREFIX}")
INSTALL(TARGETS ${name} ${g3kblib}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
        LIBRARY DESTINATION ${G3KBSWITCH_VIM_XKBSWITCH_LIB_PATH} OPTIONAL)

OPTION(G3KBSWITCH_INSTALL_PC_FILE "Install pkg-config file" ON)
IF(G3KBSWITCH_INSTALL_PC_FILE)
    IF(G3KBSWITCH_BUILD_VIM_XKBSWITCH_LIB)
        CMAKE_PATH(APPEND
                   G3KBSWITCH_PC_LIBPATH_DECL
                   "\${prefix}"
                   ${G3KBSWITCH_VIM_XKBSWITCH_LIB_PATH}
                   "$<TARGET_FILE_NAME:g3kbswitch>")
        STRING(PREPEND G3KBSWITCH_PC_LIBPATH_DECL "libpath=")
    ENDIF()
    IF(G3KBSWITCH_WITH_GNOME_SHELL_EXTENSION)
        SET(G3KBSWITCH_PC_EXTNAME_DECL "extname=g3kb-switch@g3kb-switch.org")
    ENDIF()
    CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/${name}.pc.in
                   ${CMAKE_BINARY_DIR}/${name}.pc.1.in
                   @ONLY)
    FILE(GENERATE
         OUTPUT ${CMAKE_BINARY_DIR}/${name}.pc
         INPUT ${CMAKE_BINARY_DIR}/${name}.pc.1.in)
    INSTALL(FILES ${CMAKE_BINARY_DIR}/${name}.pc
            DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/pkgconfig)
ENDIF()

OPTION(G3KBSWITCH_INSTALL_MAN_PAGE "Install man documentation" ON)
IF(G3KBSWITCH_INSTALL_MAN_PAGE)
    SET(man_page ${name}.1)
    FIND_PROGRAM(GZIP gzip)
    IF (GZIP)
        SET(man_gz ${CMAKE_BINARY_DIR}/${man_page}.gz)
        ADD_CUSTOM_TARGET(man-${name} ALL DEPENDS ${man_gz})
        ADD_CUSTOM_COMMAND(
            OUTPUT ${man_gz}
            COMMAND ${GZIP} -9 --no-name --to-stdout "${man_page}" >"${man_gz}"
            WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/docs)
        SET(man_page ${man_gz})
    ELSE()
        SET(man_page ${CMAKE_SOURCE_DIR}/docs/${man_page})
    ENDIF()
    INSTALL(FILES ${man_page}
            DESTINATION ${CMAKE_INSTALL_MANDIR}/man1)
ENDIF()

SET(CPACK_PACKAGE_CONTACT ${HOMEPAGE_URL}/issues)
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Alexey Radkov <alexey.radkov@gmail.com>")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS libglib2.0-0)
SET(CPACK_RPM_PACKAGE_REQUIRES glib2)
SET(CPACK_RPM_PACKAGE_LICENSE BSD2)
SET(CPACK_RPM_PACKAGE_URL ${HOMEPAGE_URL})
INCLUDE(CPack)
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
SET(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
